//
// This PEG file strives to capture as much of the declarative
// pipeline syntax as possible
//
pipeline = _{ SOI ~ shebang? ~
                    "pipeline" ~
                    BLOCK_BEGIN ~
                    // I am pretty sure this grammar is wrong and there has to
                    // a better way to allow these declarations anywhere within
                    // the block, but still only allow one of each
                    (
                    agentDecl
                    | environmentDecl
                    | optionsDecl
                    | postDecl
                    | toolsDecl
                    | stagesDecl{1}
                    )*
                    ~
                    BLOCK_END ~
                    EOI }

// XXX: Why won't this work!
//shebang = _{ "#!" ~ (!NEWLINE ~ ANY)* }
shebang = _{ "#!groovy" }
// Garbage is any Groovy defined outside of the pipeline block, basically
// making a mockery of Declarative pipeline :)
garbage = {
            "@" ~ (!NEWLINE ~ ANY)*
          }

agentDecl = { "agent" ~
              ("any"
              | "none"
              | agentBlock)
            }
agentBlock = {
                BLOCK_BEGIN ~
                (k8sAgent
                | dockerAgent
                | dockerfileAgent
                | nodeAgent
                | ("label" ~ string)) ~
                BLOCK_END
             }
k8sAgent = { "kubernetes" ~
             BLOCK_BEGIN ~
             ("defaultContainer" ~ string)? ~
             ("yaml" ~ string)? ~
             ("yamlFile" ~ string)? ~
             BLOCK_END
           }
dockerAgent = { "docker" ~
             BLOCK_BEGIN ~
             ("reuseNode" ~ BOOL)? ~
             ("image" ~ string)? ~
             ("label" ~ string)? ~
             ("args" ~ string)? ~
             ("registryUrl" ~ string)? ~
             ("registryCredentialsId" ~ string)? ~
             ("customWorkspace" ~ string)? ~
             BLOCK_END
           }
dockerfileAgent = { "dockerfile" ~
             BLOCK_BEGIN ~
             ("reuseNode" ~ BOOL)? ~
             ("filename" ~ string)? ~
             ("dir" ~ string)? ~
             ("label" ~ string)? ~
             ("additionalBuildArgs" ~ string)? ~
             ("args" ~ string)? ~
             ("customWorkspace" ~ string)? ~
             BLOCK_END
           }
nodeAgent = { "node" ~
             BLOCK_BEGIN ~
             ("label" ~ string)? ~
             ("customWorkspace" ~ string)? ~
             BLOCK_END
           }


environmentDecl = { "environment" ~
                    BLOCK_BEGIN ~
                    (envProperty)* ~
                    BLOCK_END
                  }
envProperty = {
                property
                | credentialProperty
              }
credentialProperty = { IDENT ~
                        "=" ~
                        "credentials(" ~ string ~ ")"
                      }

inputDecl = { "input" ~
              BLOCK_BEGIN ~
             ("message" ~ string)? ~
             ("submitter" ~ string)? ~
              BLOCK_END
            }


optionsDecl = { "options" ~
                BLOCK_BEGIN ~
                (optionFunc)* ~
                BLOCK_END
              }
optionFunc = { IDENT ~ "(" ~ args? ~ ")" }


postDecl = { "post" ~
             BLOCK_BEGIN ~
             (postBlock+) ~
             BLOCK_END
           }
postBlock = { 
            ("always"
            | "changed"
            | "fixed"
            | "regression"
            | "aborted"
            | "failure"
            | "success"
            | "unstable"
            | "unsuccessful"
            | "cleanup"
            ) ~ BLOCK_BEGIN ~ (step)+ ~ BLOCK_END }

stagesDecl = { "stages" ~
                BLOCK_BEGIN ~
                (stage)+ ~
                BLOCK_END
              }

stage = { "stage(" ~ string ~ ")" ~
            BLOCK_BEGIN ~
            agentDecl? ~
            environmentDecl? ~
            inputDecl? ~
            toolsDecl? ~
            stepsDecl ~
            postDecl? ~
            BLOCK_END
        }

stepsDecl = { "steps" ~
            BLOCK_BEGIN ~
            (step)+ ~
            BLOCK_END
            }
step = { IDENT ~ (
                    args
                    | kwargs
                    )
        }
args = { ((string | BOOL) ~ COMMA?)+ }
kwargs = _{ (kwarg ~ COMMA?)+ }
kwarg = { IDENT~ ":" ~ (string | BOOL) }
property = { IDENT ~ "=" ~ string }


toolsDecl = { "tools" ~
              BLOCK_BEGIN ~
              (IDENT ~ string)* ~
              BLOCK_END
            }

IDENT = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
BLOCK_BEGIN = @{ "{" }
BLOCK_END = @{ "}" }

string        = ${ (single_quoted | double_quoted) }
single_quoted = ${ single_quote ~ inner_single_str ~ single_quote }
double_quoted = ${ quote ~ inner_double_str ~ quote }

inner_single_str = @{ (!("'" | "\\") ~ ANY)* ~ (escape ~ inner_single_str)? }
inner_double_str = @{ (!("\"" | "\\") ~ ANY)* ~ (escape ~ inner_double_str)? }
inner_chr = @{ escape | ANY }
escape    = @{ "\\" ~ ("\"" | "\\" | "r" | "n" | "t" | "0" | "'" | code | unicode) }
code      = @{ "x" ~ hex_digit{2} }
unicode   = @{ "u" ~ opening_brace ~ hex_digit{2, 6} ~ closing_brace }
hex_digit = @{ '0'..'9' | 'a'..'f' | 'A'..'F' }

quote          = { "\"" }
single_quote   = { "'" }

assignment_operator = { "=" }
opening_brace       = { "{" }
closing_brace       = { "}" }
opening_paren       = { "(" }
closing_paren       = { ")" }
opening_brack       = { "[" }
closing_brack       = { "]" }

COMMA = @{ "," }
BOOL = @{ (TRUE | FALSE) }
TRUE = { "true" }
FALSE = { "false" }

WHITESPACE = _{ " " | "\t" | NEWLINE }
block_comment = _{ "/*" ~ (block_comment | !"*/" ~ ANY)* ~ "*/" }
COMMENT    = _{ block_comment | ("//" ~ (!NEWLINE~ ANY)*) }
