//
// This PEG file strives to capture as much of the declarative
// pipeline syntax as possible
//
pipeline = _{ SOI ~ shebang? ~
                    "pipeline" ~
                    opening_brace ~
                    // I am pretty sure this grammar is wrong and there has to
                    // a better way to allow these declarations anywhere within
                    // the block, but still only allow one of each
                    (
                    agentDecl
                    | environmentDecl
                    | optionsDecl
                    | postDecl
                    | toolsDecl
                    | triggersDecl
                    | stagesDecl{1}
                    )*
                    ~
                    closing_brace ~
                    EOI }

// XXX: Why won't this work!
//shebang = _{ "#!" ~ (!NEWLINE ~ ANY)* }
shebang = _{ "#!groovy" }
// Garbage is any Groovy defined outside of the pipeline block, basically
// making a mockery of Declarative pipeline :)
garbage = {
            "@" ~ (!NEWLINE ~ ANY)*
          }

agentDecl = { "agent" ~
              ("any"
              | "none"
              | agentBlock)
            }
agentBlock = {
                opening_brace ~
                (k8sAgent
                | dockerAgent
                | dockerfileAgent
                | nodeAgent
                | ("label" ~ string)) ~
                closing_brace
             }
k8sAgent = { "kubernetes" ~
             opening_brace ~
             ("defaultContainer" ~ string)? ~
             ("yaml" ~ string)? ~
             ("yamlFile" ~ string)? ~
             closing_brace
           }
dockerAgent = { "docker" ~
             opening_brace ~
             ("reuseNode" ~ BOOL)? ~
             ("image" ~ string)? ~
             ("label" ~ string)? ~
             ("args" ~ string)? ~
             ("registryUrl" ~ string)? ~
             ("registryCredentialsId" ~ string)? ~
             ("customWorkspace" ~ string)? ~
             closing_brace
           }
dockerfileAgent = { "dockerfile" ~
             opening_brace ~
             ("reuseNode" ~ BOOL)? ~
             ("filename" ~ string)? ~
             ("dir" ~ string)? ~
             ("label" ~ string)? ~
             ("additionalBuildArgs" ~ string)? ~
             ("args" ~ string)? ~
             ("customWorkspace" ~ string)? ~
             closing_brace
           }
nodeAgent = { "node" ~
             opening_brace ~
             ("label" ~ string)? ~
             ("customWorkspace" ~ string)? ~
             closing_brace
           }


environmentDecl = { "environment" ~
                    opening_brace ~
                    (envProperty)* ~
                    closing_brace
                  }
envProperty = {
                property
                | credentialProperty
              }
credentialProperty = { IDENT ~
                        "=" ~
                        "credentials(" ~ string ~ ")"
                      }

func = { IDENT ~ "(" ~ (func | kwargs | args)? ~ ")" }

inputDecl = { "input" ~
              opening_brace ~
             ("message" ~ string)? ~
             ("submitter" ~ string)? ~
              closing_brace
            }

optionsDecl = { "options" ~
                opening_brace ~
                (func)* ~
                closing_brace
              }

parallelDecl = { "parallel" ~
                 opening_brace ~
                 (stage)+ ~
                 closing_brace
               }

postDecl = { "post" ~
             opening_brace ~
             (postBlock+) ~
             closing_brace
           }
postBlock = { 
            ("always"
            | "changed"
            | "fixed"
            | "regression"
            | "aborted"
            | "failure"
            | "success"
            | "unstable"
            | "unsuccessful"
            | "cleanup"
            ) ~ opening_brace ~ (step)+ ~ closing_brace }

stagesDecl = { "stages" ~
                opening_brace ~
                (stage)+ ~
                closing_brace
              }

stage = { "stage(" ~ string ~ ")" ~
            opening_brace ~
            whenDecl? ~
            agentDecl? ~
            environmentDecl? ~
            inputDecl? ~
            toolsDecl? ~
            (stepsDecl | parallelDecl) ~
            postDecl? ~
            closing_brace
        }

stepsDecl = { "steps" ~
            opening_brace ~
            (step)+ ~
            closing_brace
            }


step_args = _{ args | kwargs | array_kwargs }
simple_step = _{ IDENT ~ step_args }
// A block step is something like dir('foo') { } that accepts steps within it
// in many cases these will have keyword arguments like withCredentials
block_step = _{ IDENT ~
                (opening_paren ~ step_args? ~ closing_paren)? ~
                opening_brace ~
                (step)+ ~
                closing_brace
              }
step = { simple_step | block_step }

arg = _{ string | BOOL | number }
args = { (arg ~ COMMA?)+ }
kwargs = _{ (kwarg ~ COMMA?)+ }
kwarg = { IDENT~ ":" ~ arg }

// This syntax is some Groovy invocation magic that I'm not sure I fully
// understand at the moment
array_kwargs = _{ "[[" ~ kwargs ~ "]]" }
property = { IDENT ~ "=" ~ string }


toolsDecl = { "tools" ~
              opening_brace ~
              (IDENT ~ string)* ~
              closing_brace
            }
triggersDecl = { "triggers" ~
                opening_brace ~
                (func)* ~
                closing_brace
              }
whenDecl = { "when" ~
                opening_brace ~
                ("expression" ~ opening_brace ~ (!closing_brace ~ ANY)* ~ closing_brace) ~
                closing_brace
              }


IDENT = @{ (ASCII_ALPHA | "$") ~ (ASCII_ALPHANUMERIC | "_")* }

string        = ${ (single_quoted | double_quoted) }
single_quoted = ${ single_quote ~ inner_single_str ~ single_quote }
double_quoted = ${ quote ~ inner_double_str ~ quote }

inner_single_str = @{ (!("'" | "\\") ~ ANY)* ~ (escape ~ inner_single_str)? }
inner_double_str = @{ (!("\"" | "\\") ~ ANY)* ~ (escape ~ inner_double_str)? }
inner_chr = @{ escape | ANY }
escape    = @{ "\\" ~ ("\"" | "\\" | "r" | "n" | "t" | "0" | "'" | code | unicode) }
code      = @{ "x" ~ hex_digit{2} }
unicode   = @{ "u" ~ opening_brace ~ hex_digit{2, 6} ~ closing_brace }
hex_digit = @{ '0'..'9' | 'a'..'f' | 'A'..'F' }

quote          = { "\"" }
single_quote   = { "'" }

assignment_operator = { "=" }
opening_brace       = { "{" }
closing_brace       = { "}" }
opening_paren       = { "(" }
closing_paren       = { ")" }
opening_brack       = { "[" }
closing_brack       = { "]" }

number = @{ '0'..'9'+ }
integer = @{ number | "-" ~ "0"* ~ '1'..'9' ~ number? }

COMMA = @{ "," }
BOOL = @{ (TRUE | FALSE) }
TRUE = { "true" }
FALSE = { "false" }

WHITESPACE = _{ " " | "\t" | NEWLINE }
block_comment = _{ "/*" ~ (block_comment | !"*/" ~ ANY)* ~ "*/" }
COMMENT    = _{ block_comment | ("//" ~ (!NEWLINE~ ANY)*) }
