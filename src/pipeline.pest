//
// This PEG file strives to capture as much of the declarative
// pipeline syntax as possible
//
pipeline = _{ SOI ~ "pipeline" ~
                    BLOCK_BEGIN ~
                    agentDecl? ~
                    optionsDecl? ~
                    environmentDecl? ~
                    stagesDecl ~
                    postDecl? ~
                    BLOCK_END ~ EOI }


agentDecl = { "agent" ~
              ("any"
              | "none"
              | agentBlock)
            }
agentBlock = {
                BLOCK_BEGIN ~
                (k8sAgent
                | dockerAgent
                | dockerfileAgent
                | nodeAgent) ~
                BLOCK_END
             }
k8sAgent = { "kubernetes" ~
             BLOCK_BEGIN ~
             ("defaultContainer" ~ STR)? ~
             ("yaml" ~ STR)? ~
             ("yamlFile" ~ STR)? ~
             BLOCK_END
           }
dockerAgent = { "docker" ~
             BLOCK_BEGIN ~
             ("reuseNode" ~ BOOL)? ~
             ("image" ~ STR)? ~
             ("label" ~ STR)? ~
             ("args" ~ STR)? ~
             ("registryUrl" ~ STR)? ~
             ("registryCredentialsId" ~ STR)? ~
             ("customWorkspace" ~ STR)? ~
             BLOCK_END
           }
dockerfileAgent = { "dockerfile" ~
             BLOCK_BEGIN ~
             ("reuseNode" ~ BOOL)? ~
             ("filename" ~ STR)? ~
             ("dir" ~ STR)? ~
             ("label" ~ STR)? ~
             ("additionalBuildArgs" ~ STR)? ~
             ("args" ~ STR)? ~
             ("customWorkspace" ~ STR)? ~
             BLOCK_END
           }
nodeAgent = { "node" ~
             BLOCK_BEGIN ~
             ("label" ~ STR)? ~
             ("customWorkspace" ~ STR)? ~
             BLOCK_END
           }


environmentDecl = { "environment" ~
                    BLOCK_BEGIN ~
                    (envProperty)* ~
                    BLOCK_END
                  }
envProperty = {
                property
                | credentialProperty
              }
credentialProperty = { IDENT ~
                        "=" ~
                        "credentials(" ~ STR ~ ")"
                      }

inputDecl = { "input" ~
              BLOCK_BEGIN ~
             ("message" ~ STR)? ~
             ("submitter" ~ STR)? ~
              BLOCK_END
            }


optionsDecl = { "options" ~
                BLOCK_BEGIN ~
                (optionFunc)* ~
                BLOCK_END
              }
optionFunc = { IDENT ~ "()" }


postDecl = { "post" ~
             BLOCK_BEGIN ~
             (postBlock+) ~
             BLOCK_END
           }
postBlock = { 
            ("always"
            | "changed"
            | "fixed"
            | "regression"
            | "aborted"
            | "failure"
            | "success"
            | "unstable"
            | "unsuccessful"
            | "cleanup"
            ) ~ BLOCK_BEGIN ~ (step)+ ~ BLOCK_END }

stagesDecl = { "stages" ~
                BLOCK_BEGIN ~
                (stage)+ ~
                BLOCK_END
              }

stage = { "stage(" ~ STR ~ ")" ~
            BLOCK_BEGIN ~
            agentDecl? ~
            environmentDecl? ~
            inputDecl? ~
            stepsDecl ~
            postDecl? ~
            BLOCK_END
        }

stepsDecl = { "steps" ~
            BLOCK_BEGIN ~
            (step)+ ~
            BLOCK_END
            }

step = { IDENT ~ (
                    args
                    | kwargs
                    )
        }

args = { (STR ~ COMMA?)+ }
kwargs = _{ (kwarg ~ COMMA?)+ }
kwarg = { IDENT~ ":" ~ (STR | BOOL) }
property = { IDENT ~ "=" ~ STR }

IDENT = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
BLOCK_BEGIN = @{ "{" }
BLOCK_END = @{ "}" }
STR = { "'" ~ STRV ~ "'" }
STRV = @{ "''" | (!"'" ~ ANY)* }
COMMA = @{ "," }
BOOL = @{ (TRUE | FALSE) }
TRUE = { "true" }
FALSE = { "false" }

WHITESPACE = _{ (" " | NEWLINE) }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }
