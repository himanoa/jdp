//
// This PEG file strives to capture as much of the declarative
// pipeline syntax as possible
//
pipeline = _{ SOI ~ "pipeline" ~
                    BLOCK_BEGIN ~
                    agentDecl? ~
                    optionsDecl? ~
                    environmentDecl? ~
                    stagesDecl ~
                    BLOCK_END ~ EOI }


agentDecl = { "agent" ~
              ("any"
              | "none"
              | agentBlock)
            }
agentBlock = {
                BLOCK_BEGIN ~
                (k8sAgent
                | dockerAgent
                | dockerfileAgent
                | nodeAgent) ~
                BLOCK_END
             }
k8sAgent = { "kubernetes" ~
             BLOCK_BEGIN ~
             ("defaultContainer" ~ STR)? ~
             ("yaml" ~ STR)? ~
             ("yamlFile" ~ STR)? ~
             BLOCK_END
           }
dockerAgent = { "docker" ~
             BLOCK_BEGIN ~
             ("reuseNode" ~ BOOL)? ~
             ("image" ~ STR)? ~
             ("label" ~ STR)? ~
             ("args" ~ STR)? ~
             ("registryUrl" ~ STR)? ~
             ("registryCredentialsId" ~ STR)? ~
             ("customWorkspace" ~ STR)? ~
             BLOCK_END
           }
dockerfileAgent = { "dockerfile" ~
             BLOCK_BEGIN ~
             ("reuseNode" ~ BOOL)? ~
             ("filename" ~ STR)? ~
             ("dir" ~ STR)? ~
             ("label" ~ STR)? ~
             ("additionalBuildArgs" ~ STR)? ~
             ("args" ~ STR)? ~
             ("customWorkspace" ~ STR)? ~
             BLOCK_END
           }
nodeAgent = { "node" ~
             BLOCK_BEGIN ~
             ("label" ~ STR)? ~
             ("customWorkspace" ~ STR)? ~
             BLOCK_END
           }


environmentDecl = { "environment" ~
                    BLOCK_BEGIN ~
                    (envProperty)* ~
                    BLOCK_END
                  }
envProperty = {
                property
                | credentialProperty
              }
credentialProperty = { IDENT ~
                        "=" ~
                        "credentials(" ~ STR ~ ")"
                      }


optionsDecl = { "options" ~
                BLOCK_BEGIN ~
                (optionFunc)* ~
                BLOCK_END
              }
optionFunc = { IDENT ~ "()" }

stagesDecl = { "stages" ~
                BLOCK_BEGIN ~
                (stage)+ ~
                BLOCK_END
              }

stage = { "stage(" ~ STR ~ ")" ~
            BLOCK_BEGIN ~
            agentDecl? ~
            environmentDecl? ~
            stepsDecl ~
            BLOCK_END
        }

stepsDecl = { "steps" ~
            BLOCK_BEGIN ~
            (step)+ ~
            BLOCK_END
            }

step = { IDENT ~ (
                    args
                    | kwargs
                    )
        }

args = { (STR ~ COMMA?)+ }
kwargs = _{ (kwarg ~ COMMA?)+ }
kwarg = { IDENT~ ":" ~ STR }
property = { IDENT ~ "=" ~ STR }

IDENT = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
BLOCK_BEGIN = @{ "{" }
BLOCK_END = @{ "}" }
STR = { "'" ~ STRV ~ "'" }
STRV = @{ "''" | (!"'" ~ ANY)* }
COMMA = @{ "," }
BOOL = @{ (TRUE | FALSE) }
TRUE = { "true" }
FALSE = { "false" }

WHITESPACE = _{ (" " | NEWLINE) }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }
